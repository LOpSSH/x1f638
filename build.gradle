// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            appId            : 'io.github.rockerhieu.x1f638',
            minSdk           : 16,
            targetSdk        : 26,
            compileSdk       : 26,
            buildTools       : '26.0.2',
            androidGradle    : '3.0.0-beta7',
            androidSupport   : '26.1.0',
            kotlin           : '1.1.51',
            butterKnife      : '8.8.1',
            rxJava           : '1.3.0',
            rxAndroid        : '1.2.1',
            rxLifecycle      : '0.3.0',
            rxBinding        : '0.4.0',
            dart             : '2.0.2',
            dagger           : '2.11',
            fabricGradle     : '1.24.1',
            crashlytics      : '2.6.8@aar',
            versionberg      : '1.0.0',
            junit            : '4.12',
            mockito          : '2.9.0',
            robolectric      : '3.4.2',
            androidTestRunner: '0.5',
            espresso         : '2.2.2',
    ]
    ext.deps = [
            android    : [
                    gradle: "com.android.tools.build:gradle:${versions.androidGradle}",
            ],
            support    : [
                    appCompat       : "com.android.support:appcompat-v7:${versions.androidSupport}",
                    annotations     : "com.android.support:support-annotations:${versions.androidSupport}",
                    constraintLayout: "com.android.support.constraint:constraint-layout:${versions.constraintLayout}",
                    design          : "com.android.support:design:${versions.androidSupport}",
            ],
            databinding: [
                    compiler: "com.android.databinding:compiler:${versions.androidGradle}",
            ],
            kotlin     : [
                    gradle: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
                    std   : "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}",
            ],
            butterKnife: [
                    core    : "com.jakewharton:butterknife:${versions.butterKnife}",
                    compiler: "com.jakewharton:butterknife-compiler:${versions.butterKnife}",
            ],
            reactivex  : [
                    rxJava   : "io.reactivex:rxjava:${versions.rxJava}",
                    rxAndroid: "io.reactivex:rxandroid:${versions.rxAndroid}",
            ],
            rxLifecycle: [
                    core      : "com.trello:rxlifecycle:${versions.rxLifecycle}",
                    components: "com.trello:rxlifecycle-components:${versions.rxLifecycle}",
            ],
            rxBinding  : "com.jakewharton.rxbinding:rxbinding:${versions.rxBinding}",
            dart       : [
                    core    : "com.f2prateek.dart:dart:${versions.dart}",
                    compiler: "com.f2prateek.dart:dart-processor:${versions.dart}",
            ],
            dagger     : [
                    core    : "com.google.dagger:dagger:${versions.dagger}",
                    compiler: "com.google.dagger:dagger-compiler:${versions.dagger}",
            ],
            fabric     : [
                    gradle     : "io.fabric.tools:gradle:${versions.fabricGradle}",
                    crashlytics: "com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}",
            ],
            versionberg: "io.github.rockerhieu:versionberg:${versions.versionberg}",
            junit      : "junit:junit:${versions.junit}",
            mockito    : "org.mockito:mockito-core:${versions.mockito}",
            robolectric: [
                    core     : "org.robolectric:robolectric:${versions.robolectric}",
                    multiDex : "org.robolectric:multidex:${versions.robolectric}",
                    gms      : "org.robolectric:playservices:${versions.robolectric}",
                    supportV4: "org.robolectric:supportv4:${versions.robolectric}",
            ],
            androidTest: [
                    runner      : "com.android.support.test:runner:${versions.androidTestRunner}",
                    espressoCore: "com.android.support.test.espresso:espresso-core:${versions.espresso}",
            ]
    ]
    ext.repos = [
            fabric    : 'https://maven.fabric.io/public',
            rockerhieu: 'https://raw.github.com/rockerhieu/mvn-repo/master/',
            snapshot  : 'https://oss.sonatype.org/content/repositories/snapshots/',
    ]
    repositories {
        jcenter()
        google()
        maven { url repos.fabric }
    }
    dependencies {
        classpath deps.android.gradle
        classpath deps.fabric.gradle
        classpath deps.kotlin.gradle
        classpath deps.versionberg
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
